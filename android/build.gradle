// android/build.gradle

// based on:
//
// * https://github.com/facebook/react-native/blob/0.60-stable/template/android/build.gradle
//   original location:
//   - https://github.com/facebook/react-native/blob/0.58-stable/local-cli/templates/HelloWorld/android/build.gradle
//
// * https://github.com/facebook/react-native/blob/0.60-stable/template/android/app/build.gradle
//   original location:
//   - https://github.com/facebook/react-native/blob/0.58-stable/local-cli/templates/HelloWorld/android/app/build.gradle

def DEFAULT_COMPILE_SDK_VERSION = 34
def DEFAULT_BUILD_TOOLS_VERSION = '34.0.0'
def DEFAULT_MIN_SDK_VERSION = 21
def DEFAULT_TARGET_SDK_VERSION = 34

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    // ref: https://docs.gradle.org/current/userguide/tutorial_using_tasks.html#sec:build_script_external_dependencies
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:8.1.0'
        }
    }
}

// 获取React Native和SVGAPlayer版本，允许外部项目覆盖
def DEFAULT_REACT_NATIVE_VERSION = safeExtGet('reactNativeVersion', '0.76.5')
def DEFAULT_SVGA_PLAYER_VERSION = safeExtGet('svgaPlayerVersion', '2.6.1')

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError = false
    }
    buildFeatures {
        buildConfig = true
    }
    namespace = "bang.svga.player"
}

repositories {
    // ref: https://www.baeldung.com/maven-local-repository
    mavenLocal()
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // 使用compileOnly声明React Native依赖，避免与主项目版本冲突
    compileOnly "com.facebook.react:react-android:${DEFAULT_REACT_NATIVE_VERSION}"
    
    // 使用api声明SVGAPlayer依赖，明确表示这是必需的传递依赖
    api "com.github.yyued:SVGAPlayer-Android:${DEFAULT_SVGA_PLAYER_VERSION}"
}
